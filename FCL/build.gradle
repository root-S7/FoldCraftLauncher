import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id "org.hidetake.ssh" version "2.11.2"
}

android {
    namespace 'com.tungsten.fcl'
    compileSdk 34

    Properties properties = new Properties()
    properties.load(Files.newInputStream(Paths.get("${rootDir}/local.properties")))

    defaultConfig {
        signingConfigs {
            FCLDebugKey {
                storeFile file("../debug-key.jks")
                storePassword "FCL-Debug"
                keyAlias "FCL-Debug"
                keyPassword "FCL-Debug"
            }
        }
        aaptOptions {
            ignoreAssetsPattern '!bin-arm.tar.xz*:!bin-x86.tar.xz*:!bin-x86_64.tar.xz*'
        }
        //包名建议使用"个人域名反写+服务器名称取每个字第一个字母"
        /**
         * 比如我有个域名是"mcbbs.ren",我的服务器名称叫"猫窝"
         * 那么这里的"applicationId"应该写成"ren.icraft.mw"
         *
         * 注意：手机厂商会根据APK包名给应用不同性能策略
         * 如果你需要更高的帧率那么可修改成其他游戏包名或者跑分APP包名
        **/
        applicationId "com.hg.wreckfest"
        minSdk 26
        targetSdk 34
        versionCode 1195
        versionName "1.1.9.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilter "arm64-v8a"
            }
        }
        configureEach {
            resValue "string", "app_version", "${defaultConfig.versionName}"
            resValue "string", "curse_api_key", properties.get("curse-api-key","null")
            resValue "string", "oauth_api_key", properties.get("oauth-api-key","null")
        }
    }
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "FCL-${variant.buildType.name}-${defaultConfig.versionName}.APK"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
            pickFirsts += ['**/libbytehook.so']
        }
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    dataBinding {
        enabled true
    }
    buildFeatures {
        buildConfig true
    }
    splits {
        def arch = System.getProperty("arch", "all")
        if (arch != 'all') {
            abi {
                enable true
                reset()
                switch (arch) {
                    case 'arm':
                        include 'armeabi-v7a'
                        break
                    case 'arm64':
                        include 'arm64-v8a'
                        break
                    case 'x86':
                        include 'x86'
                        break
                    case 'x86_64':
                        include 'x86_64'
                        break
                }
            }
        }
    }
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':FCLCore')
    implementation project(path: ':FCLLibrary')
    implementation project(path: ':FCLauncher')
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.14.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.apache.commons:commons-compress:1.25.0'
    implementation 'org.tukaani:xz:1.9'
    implementation 'com.github.steveice10:opennbt:1.5'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.github.Mathias-Boulay:android_gamepad_remapper:2.0.3'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'com.jaredrummler:android-device-names:2.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}